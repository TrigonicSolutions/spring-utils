apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'

group = 'com.trigonic'
artifact = 'spring-utils'
version = '0.6'

def props = new Properties();
def localProperties = new File("local.properties")
if (localProperties.exists()) localProperties.withInputStream { props.load(it) }
props.each { project.setProperty(it.key, it.value) }

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    mavenRepo urls: 'http://reflections.googlecode.com/svn/repo'
    mavenLocal()
}

dependencies {
    def springVersion = '3.0.5.RELEASE'

    compile "org.springframework:spring-beans:${springVersion}"
    compile "org.springframework:spring-context:${springVersion}"
    compile "org.springframework:spring-web:${springVersion}"
    compile 'javax.servlet:servlet-api:2.5'

    testCompile 'junit:junit:4.+'
    testCompile 'org.reflections:reflections:0.9.+'
    testCompile 'org.easymock:easymock:3.0'
    testCompile 'com.trigonic:test-utils:0.2'

    deployerJars 'org.apache.maven.wagon:wagon-http:1.0-beta-7'
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: props.get('maven.repository.user'), password: props.get('maven.repository.password'))
        }

        pom.project {
            name 'spring-utils - Spring Framework Utilities'
            packaging 'jar'
            description 'Provides utilities for and extensions of the Spring Framework'
            url 'https://github.com/TrigonicSolutions/spring-utils'
            inceptionYear '2011'

            parent {
                groupId 'org.sonatype.oss'
                artifactId 'oss-parent'
                version '7'
            }
     
            scm {
                url 'scm:git://github.com/TrigonicSolutions/spring-utils.git'
                connection 'scm:git://github.com/TrigonicSolutions/spring-utils.git'
            }
     
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
     
            developers {
                developer {
                    id 'akrueger'
                    name 'Alan Krueger'
                    email 'alan@trigonic.com'
                    roles{
                        role 'Developer'
                    }
                    timezone '-6'
                }
            }
        }
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-6'
}

